import io.izzel.arclight.gradle.tasks.UploadFilesTask

allprojects {
    apply plugin: 'java'

    group 'io.izzel.arclight'
    version '1.0.0-SNAPSHOT'

    def getGitHash = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }

    ext {
        agpVersion = '1.23'
        minecraftVersion = '1.19.2'
        forgeVersion = '43.4.0'
        apiVersion = '1.7.3'
        toolsVersion = '1.3.0'
        mixinVersion = '0.8.5'
        mixinToolsVersion = '1.2.4'
        versionName = 'horn'
        gitHash = getGitHash()
    }

    java.toolchain.languageVersion = JavaLanguageVersion.of(17)
    
    repositories {
        maven { url = 'https://repo.spongepowered.org/maven' }
        maven { url = 'https://oss.sonatype.org/content/repositories/snapshots/' }
        maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
        maven { url = 'https://maven.neoforged.net/releases' }
        maven { url = 'https://files.minecraftforge.net/maven/' }
        maven { url = 'https://maven.izzel.io/releases' }
        maven { url = 'https://jitpack.io/' }
        mavenCentral()
        mavenLocal()
    }

   dependencies {
        compileOnly 'org.spigotmc:spigot-api:1.19.2-R0.1-SNAPSHOT'
        compileOnly 'org.spigotmc:spigot:1.19.2-R0.1-SNAPSHOT'
    }
    
    task cleanBuild {
        doFirst {
            project.file("build/libs").deleteDir()
        }
    }
}

jar.enabled = false

task collect(type: Copy) {
    destinationDir = file('build/libs')
    from { project(':arclight-forge').tasks.jar.outputs }
    dependsOn { project(':arclight-forge').tasks.jar }
}

def gitBranch() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--abbrev-ref', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

tasks.register('uploadFiles', UploadFilesTask) {
    mcVersion.set project.ext.minecraftVersion
    version.set "${project.version}-${project.ext.gitHash}"
    snapshot.set project.version.toString().endsWith("-SNAPSHOT")
    gitHash.set project.ext.gitHash
    branch.set gitBranch()
    inputs.files tasks.collect.outputs.files
    dependsOn(tasks.collect)
}
